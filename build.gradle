buildscript {
    ext {
        springBootVersion = '1.2.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
        classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot' 
apply plugin: 'io.spring.dependency-management' 

jar {
    baseName = 'masterSpringMvc'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

configurations {
    testCompile {
        exclude module: 'selenium-safari-driver'
        exclude module: 'selenium-ie-driver'
        //exclude module: 'selenium-firefox-driver'
        exclude module: 'selenium-htmlunit-driver'
        exclude module: 'selenium-chrome-driver'
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-actuator:1.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf:1.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-social-twitter:1.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security:1.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-redis:1.2.5.RELEASE'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.2.5.RELEASE'

    compile 'org.springframework.session:spring-session:1.0.2.RELEASE'

    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity3:2.1.2.RELEASE'

    compile 'org.webjars:materializecss:0.97.0'
    compile 'org.webjars:jquery:2.1.4'

    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.1'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.6.1'

    compile 'com.jayway.jsonpath:json-path:2.0.0'

    testCompile 'org.assertj:assertj-core:3.1.0'

    testCompile 'org.fluentlenium:fluentlenium-assertj:0.10.3'

    testCompile 'com.codeborne:phantomjsdriver:1.2.1'

    testCompile 'org.seleniumhq.selenium:selenium-java:2.47.1'

    testCompile 'org.codehaus.groovy:groovy-all:2.4.4:indy'

    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'

    testCompile 'org.gebish:geb-spock:0.12.2'
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.destination = file("${reporting.baseDir}/integrationTests")
}
